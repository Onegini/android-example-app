/*
 * Copyright (c) 2016-2017 Onegini B.V.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

allprojects {
  apply plugin: 'maven'
  repositories {
    mavenLocal()
    mavenCentral()
    // REQUIRED: Google's new Maven repo is required for the latest
    // support library that is compatible with Android 8.0
    maven {
      url 'https://maven.google.com'
    }
    if (project.hasProperty('artifactory_user') && project.hasProperty('artifactory_password')) {
      maven {
        url "https://repo.onegini.com/artifactory/public"
        credentials {
          username artifactory_user
          password artifactory_password
        }
      }
    } else {
      throw new InvalidUserDataException("You must configure the 'artifactory_user' and 'artifactory_password' properties in your project before you can " +
          "build it.")
    }
  }
}

android {
  compileSdkVersion 26
  buildToolsVersion '26.0.2'

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    applicationId "com.onegini.mobile.exampleapp"
    minSdkVersion 16
    targetSdkVersion 26
    versionCode 10
    versionName "4.0.0-BETA"
    multiDexEnabled true
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-onegini-sdk.pro', 'proguard-rules-example-app.pro'
    }
  }
  dexOptions {
    javaMaxHeapSize "4g"
  }
  packagingOptions {
    exclude 'META-INF/rxjava.properties'
  }
  lintOptions {
    abortOnError false
  }
}

dependencies {
  // Onegini SDK
  compile('com.onegini.mobile.sdk.android:onegini-sdk:8.0.0-SNAPSHOT@aar') {
    transitive = true
  }

  // Rest Client
  compile 'com.squareup.retrofit:retrofit:1.9.0'
  compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
  // we have to use this wrapper for Retrofit 1 + OkHttp 3 usage
  compile 'com.jakewharton.retrofit:retrofit1-okhttp3-client:1.1.0'

  // Google Support
  compile 'com.android.support:appcompat-v7:26.1.0'
  compile 'com.android.support:cardview-v7:26.1.0'
  compile 'com.android.support:recyclerview-v7:26.1.0'

  // Google Play Services availability check
  compile 'com.google.android.gms:play-services-base:11.0.2'
  //Firebase Cloud Messaging (FCM)
  compile "com.google.firebase:firebase-messaging:11.0.2"
  // Support for multidex
  compile 'com.android.support:multidex:1.0.1'
  // DI for views
  compile 'com.jakewharton:butterknife:8.8.1'
  annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
  // RxJava
  compile 'io.reactivex:rxandroid:1.2.1'
  compile 'io.reactivex:rxjava:1.2.3'
}

//according to google documentation, this plugin has to be applied at the end of the file
apply plugin: 'com.google.gms.google-services'