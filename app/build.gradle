/*
 * Copyright (c) 2016-2018 Onegini B.V.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

allprojects {
  apply plugin: 'maven'
  repositories {
    mavenLocal()
    mavenCentral()
    // REQUIRED: Google's new Maven repo is required for the latest
    // support library that is compatible with Android 8.0
    google()
    if (project.hasProperty('artifactory_user') && project.hasProperty('artifactory_password')) {
      maven {
        /*
        Before the release please change the url below to: https://repo.onegini.com/artifactory/onegini-sdk
        Please change it back to https://repo.onegini.com/artifactory/public after the release
        */
        url "https://repo.onegini.com/artifactory/public"
        credentials {
          username artifactory_user
          password artifactory_password
        }
      }
    } else {
      throw new InvalidUserDataException("You must configure the 'artifactory_user' and 'artifactory_password' properties in your project before you can " +
          "build it.")
    }
  }
}

android {
  compileSdkVersion 28
  buildToolsVersion '28.0.3'

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    applicationId "com.onegini.mobile.exampleapp"
    minSdkVersion 16
    targetSdkVersion 28
    versionCode 16
    versionName "5.5.0"
    multiDexEnabled true
  }
  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-example-app.pro'
    }
  }
  dexOptions {
    javaMaxHeapSize "4g"
  }
  packagingOptions {
    exclude 'META-INF/rxjava.properties'
  }
  lintOptions {
    abortOnError false
  }
}

dependencies {
  // Onegini SDK
  api('com.onegini.mobile.sdk.android:onegini-sdk:9.5.2-SNAPSHOT@aar') {
    transitive = true
  }

  // Rest Client
  implementation 'com.squareup.retrofit2:adapter-rxjava:2.5.0'

  // Google Support
  implementation 'com.android.support:appcompat-v7:28.0.0'
  implementation 'com.android.support:cardview-v7:28.0.0'
  implementation 'com.android.support:design:28.0.0'
  implementation 'com.android.support:recyclerview-v7:28.0.0'
  implementation 'com.android.support:support-core-ui:28.0.0'
  implementation 'com.android.support:support-media-compat:28.0.0'
  implementation 'com.android.support:support-v4:28.0.0'

  // QR code reader
  implementation 'com.google.android.gms:play-services-vision:17.0.2'
  // Google Play Services availability check
  implementation 'com.google.android.gms:play-services-base:16.0.1'
  //Firebase Cloud Messaging (FCM)
  implementation 'com.google.firebase:firebase-messaging:17.6.0'
  // Support for multidex
  implementation 'com.android.support:multidex:1.0.2'
  // DI for views
  implementation 'com.jakewharton:butterknife:8.8.1'
  annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
  // RxJava
  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
  implementation 'io.reactivex.rxjava2:rxjava:2.2.6'
}

//according to google documentation, this plugin has to be applied at the end of the file
apply plugin: 'com.google.gms.google-services'